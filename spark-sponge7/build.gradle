buildscript {
    repositories {
        maven {
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'net.kyori.blossom' version '1.3.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

sourceSets {
    main {
        ext.refMap = "mixins.spark.refmap.json"
    }
}

mixin {
    add sourceSets.main, "mixins.spark.refmap.json"
}

minecraft {
    mappings channel: 'snapshot', version: '20180814-1.12'
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft 'net.minecraft:joined:1.12.2'
	shade project(':spark-common')

    compileOnly 'org.spongepowered:spongeapi:7.4.0'
    annotationProcessor 'org.spongepowered:spongeapi:7.4.0'

    compileOnly 'org.spongepowered:mixin:0.8.5'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

repositories {
    maven { url "https://repo.spongepowered.org/maven" }
}

blossom {
    replaceTokenIn('src/main/java/me/lucko/spark/sponge/Sponge7SparkPlugin.java')
    replaceToken '@version@', project.pluginVersion
    replaceToken '@desc@', project.pluginDescription
}

jar {
    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.spark.json'
    )
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-sponge7.jar"
	configurations = [project.configurations.shade]

    relocate 'okio', 'me.lucko.spark.lib.okio'
    relocate 'okhttp3', 'me.lucko.spark.lib.okhttp3'
    relocate 'net.kyori.adventure', 'me.lucko.spark.lib.adventure'
    relocate 'net.kyori.examination', 'me.lucko.spark.lib.adventure.examination'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'org.tukaani.xz', 'me.lucko.spark.lib.xz'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
    exclude 'META-INF/versions/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}

reobf {
    shadowJar {}
}
